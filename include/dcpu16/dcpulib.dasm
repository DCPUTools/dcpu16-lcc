:__dispptr
DAT 0x8000
:__isprintf
DAT 0x0000
;
;extern unsigned putchar( unsigned );
;
:_global_putchar
SET B, [__dispptr]
SET C, A
AND C, 0x007F
IFN C, 0x000A
SET PC, __putchar_cont
JSR __newline
SET [__dispptr], B
SET PC, POP
:__putchar_cont
SET [B], A
ADD [__dispptr], 1
SET PC, POP
;
;extern int puts( const char* );
;
:_global_puts
SET B, [__dispptr]
SET C, 0
:__puts_loop
IFN [A], 0x000A ;'\n'
SET PC, __puts_std
JSR __newline
SET PC, __puts_cont
ADD A, 1
:__puts_std
SET [B], [A]
BOR [B], 0xF000
ADD B, 1
:__puts_cont
IFG B, 0x817F
SUB B, 0x0180
ADD A, 1
ADD C, 1
IFN [A], 0
SET PC, __puts_loop
:__puts_done
IFN [__isprintf], 0
SET PC, __puts_cont2
JSR __newline               ;newline at the end
ADD C, 1
:__puts_cont2
SET [__isprintf], 0
SET A, C
SET [__dispptr], B
SET PC, POP
:__newline
SET I, B
MOD I, 0x0020
ADD B, 0x0020
SUB B, I
IFG B, 0x817F
SUB B, 0x0180
SET PC, POP
;
;extern char* itoa( int, char*, int );
;
:_global_itoa
SET PUSH, B
IFG 0x7FFE, A
SET PC, __itoa_cont
SET [B], 45
ADD B, 1
SET I, 0
SUB I, A
SET A, I
:__itoa_cont
JSR _global_utoa
SET A, POP
SET PC, POP
;
;extern char* utoa( unsigned, char*, int );
;
:_global_utoa
SET PUSH, X
SET PUSH, Z
SET X, B
:__itoa_loop
SET I, A
MOD I, C
ADD I, 48
IFG I, 57
ADD I, 39
SET [B], I
ADD B, 1
DIV A, C
IFN A, 0
SET PC, __itoa_loop
SET [B], 0
SUB B, 1
SET A, X
SET Z, B
SUB Z, X
SHR Z, 1
ADD Z, 1
ADD Z, X
:__itoa_loop2
SET C, [A]
SET [A], [B]
SET [B], C
ADD A, 1
SUB B, 1
IFN A, Z
SET PC, __itoa_loop2
SET A, X
SET Z, POP
SET X, POP
SET PC, POP
